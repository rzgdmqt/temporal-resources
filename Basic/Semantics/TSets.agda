--------------------------------------------------------
-- Time-varying sets (covariant presheaves on (â„•,â‰¤)), --
-- their morphisms, and basic categorical structures  --
--------------------------------------------------------

open import Function

open import Data.Empty
open import Data.Product renaming (map to mapË£)
open import Data.Sum renaming (map to mapâº)
open import Data.Unit hiding (_â‰¤_)

open import Util.Equality
open import Util.Time

module Semantics.TSets where

-- Time-varying sets (covariant presheaves on (â„•,â‰¤))

record TSet : Setâ‚ where
  constructor
    tset
  field
    -- object map / carrier of the presheaf
    carrier  : Time â†’ Set
    -- functorial action / monotonicity on (â„•,â‰¤) of the presheaf
    monotone : âˆ€ {t t'} â†’ t â‰¤ t' â†’ carrier t â†’ carrier t'
    -- functorial action preserves identities and composition
    monotone-refl  : âˆ€ {t}
                   â†’ (x : carrier t) â†’ monotone (â‰¤-refl {t}) x â‰¡ x
    monotone-trans : âˆ€ {t t' t''} â†’ (p : t â‰¤ t') â†’ (q : t' â‰¤ t'')
                   â†’ (x : carrier t) â†’ monotone q (monotone p x) â‰¡ monotone (â‰¤-trans p q) x

open TSet public

-- Constant time-varying sets

ConstTSet : Set â†’ TSet
ConstTSet A = tset (Î» _ â†’ A) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)

-- Maps of time-varying sets

record _â†’áµ—_ (A B : TSet) : Set where
  constructor
    tset-map
  field
    -- carrier of a map between two presheaves
    map-carrier : âˆ€ {t} â†’ carrier A t â†’ carrier B t
    -- naturality of the map
    map-nat : âˆ€ {t t'} â†’ (p : t â‰¤ t')
            â†’ (x : carrier A t)
            â†’ map-carrier (monotone A p x) â‰¡ monotone B p (map-carrier x)

    -- TODO: also include naturality law

infix 20 _â†’áµ—_

open _â†’áµ—_ public

-- Equality of TSet-morphisms

_â‰¡áµ—_ : âˆ€ {A B} â†’ A â†’áµ— B â†’ A â†’áµ— B â†’ Set
_â‰¡áµ—_ {A} f g = âˆ€ {t} â†’ (x : carrier A t) â†’ map-carrier f x â‰¡ map-carrier g x

infix 5 _â‰¡áµ—_

-- â‰¡áµ— implies â‰¡

â‰¡áµ—-â‰¡ : âˆ€ {A B} â†’ {f : A â†’áµ— B} {g : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ f â‰¡ g
â‰¡áµ—-â‰¡ p =
  dcongâ‚‚
    tset-map
      (ifun-ext (fun-ext p))
      (ifun-ext (ifun-ext (fun-ext (Î» q â†’ fun-ext (Î» x â†’ uip)))))

-- Begin-qed style reasoning for â‰¡áµ—

infix  3 _âˆ
infixr 2 _â‰¡âŸ¨âŸ©_ step-â‰¡
infix  1 begin_

begin_ : âˆ€ {A B} {f g : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ f â‰¡áµ— g
begin_ fâ‰¡g = fâ‰¡g

_â‰¡âŸ¨âŸ©_ : âˆ€ {A B} (f {g} : A â†’áµ— B) â†’ f â‰¡áµ— g â†’ f â‰¡áµ— g
_ â‰¡âŸ¨âŸ© fâ‰¡g = fâ‰¡g

step-â‰¡ : âˆ€ {A B} (f {g h} : A â†’áµ— B) â†’ g â‰¡áµ— h â†’ f â‰¡áµ— g â†’ f â‰¡áµ— h
step-â‰¡ _ gâ‰¡h fâ‰¡g = Î» x â†’ trans (fâ‰¡g x) (gâ‰¡h x)

_âˆ : âˆ€ {A B} (f : A â†’áµ— B) â†’ f â‰¡áµ— f
_âˆ _ = Î» x â†’ refl

syntax step-â‰¡ f gâ‰¡h fâ‰¡g = f â‰¡âŸ¨ fâ‰¡g âŸ© gâ‰¡h

-- Identity and composition of maps

idáµ— : âˆ€ {A} â†’ A â†’áµ— A
idáµ— = tset-map id (Î» p x â†’ refl)

_âˆ˜áµ—_ : âˆ€ {A B C} â†’ B â†’áµ— C â†’ A â†’áµ— B â†’ A â†’áµ— C
g âˆ˜áµ— f =
  tset-map
    (map-carrier g âˆ˜ map-carrier f)
    (Î» p x â†’
      trans
        (cong (Î» y â†’ map-carrier g y) (map-nat f p x))
        (map-nat g p (map-carrier f x)))

infixr 9 _âˆ˜áµ—_

-- Product, sum, exponent, etc structures

---- terminal object

ğŸ™áµ— : TSet
ğŸ™áµ— = tset (Î» _ â†’ âŠ¤) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)

terminaláµ— : âˆ€ {A} â†’ A â†’áµ— ğŸ™áµ—
terminaláµ— = tset-map (Î» _ â†’ tt) (Î» p x â†’ refl)

---- initial object

ğŸ˜áµ— : TSet
ğŸ˜áµ— = tset (Î» _ â†’ âŠ¥) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)

initialáµ— : âˆ€ {A} â†’ ğŸ˜áµ— â†’áµ— A
initialáµ— = tset-map (Î» ()) (Î» { p () })

---- binary products

_Ã—áµ—_ : TSet â†’ TSet â†’ TSet
A Ã—áµ— B =
  tset
    (Î» t â†’ carrier A t Ã— carrier B t)
    (Î» p â†’ mapË£ (monotone A p) (monotone B p))
    (Î» x â†’ congâ‚‚ _,_ (monotone-refl A (projâ‚ x)) (monotone-refl B (projâ‚‚ x)))
    (Î» p q x â†’ congâ‚‚ _,_ (monotone-trans A p q (projâ‚ x)) (monotone-trans B p q (projâ‚‚ x)))

infixr 23 _Ã—áµ—_

fstáµ— : âˆ€ {A B} â†’ A Ã—áµ— B â†’áµ— A
fstáµ— = tset-map projâ‚ (Î» { p (x , y) â†’ refl })

sndáµ— : âˆ€ {A B} â†’ A Ã—áµ— B â†’áµ— B
sndáµ— = tset-map projâ‚‚ (Î» { p (x , y) â†’ refl })

âŸ¨_,_âŸ©áµ— : âˆ€ {A B C} â†’ A â†’áµ— B â†’ A â†’áµ— C â†’ A â†’áµ— B Ã—áµ— C
âŸ¨ f , g âŸ©áµ— =
  tset-map
    < map-carrier f , map-carrier g >
    (Î» p x â†’ congâ‚‚ _,_ (map-nat f p x) (map-nat g p x))

mapË£áµ— : âˆ€ {A B C D} â†’ A â†’áµ— C â†’ B â†’áµ— D â†’ A Ã—áµ— B â†’áµ— C Ã—áµ— D
mapË£áµ— f g = âŸ¨ f âˆ˜áµ— fstáµ— , g âˆ˜áµ— sndáµ— âŸ©áµ—

Ã—-assocáµ— : âˆ€ {A B C} â†’ A Ã—áµ— (B Ã—áµ— C) â†’áµ— (A Ã—áµ— B) Ã—áµ— C
Ã—-assocáµ— = âŸ¨ âŸ¨ fstáµ— , fstáµ— âˆ˜áµ— sndáµ— âŸ©áµ— , sndáµ— âˆ˜áµ— sndáµ— âŸ©áµ—

Ã—-assocáµ—â»Â¹ : âˆ€ {A B C} â†’ (A Ã—áµ— B) Ã—áµ— C â†’áµ— A Ã—áµ— (B Ã—áµ— C)
Ã—-assocáµ—â»Â¹ = âŸ¨ fstáµ— âˆ˜áµ— fstáµ— , âŸ¨ sndáµ— âˆ˜áµ— fstáµ— , sndáµ— âŸ©áµ— âŸ©áµ—

---- Set-indexed products

Î  : (I : Set) â†’ (I â†’ TSet) â†’ TSet
Î  I A =
  tset
    (Î» Ï„ â†’ (i : I) â†’ carrier (A i) Ï„)
    (Î» p f i â†’ monotone (A i) p (f i))
    (Î» f â†’ fun-ext (Î» i â†’ monotone-refl (A i) (f i)))
    (Î» p q f â†’ fun-ext (Î» i â†’ monotone-trans (A i) p q (f i)))

projáµ— : âˆ€ {I A} â†’ (i : I) â†’ Î  I A â†’áµ— A i
projáµ— i =
  tset-map
    (Î» f â†’ f i)
    (Î» p f â†’ refl)
    
âŸ¨_âŸ©áµ¢áµ— : âˆ€ {A I B} â†’ ((i : I) â†’ A â†’áµ— B i) â†’ A â†’áµ— Î  I B
âŸ¨ fs âŸ©áµ¢áµ— =
  tset-map
    (Î» x i â†’ map-carrier (fs i) x)
    (Î» p x â†’ fun-ext (Î» i â†’ map-nat (fs i) p x))

mapâ±Ë£áµ— : âˆ€ {I A B} â†’ ((i : I) â†’ A i â†’áµ— B i) â†’ Î  I A â†’áµ— Î  I B
mapâ±Ë£áµ— fs = âŸ¨ (Î» i â†’ fs i âˆ˜áµ— projáµ— i) âŸ©áµ¢áµ—

---- covariant hom-functor

homáµ’ : Time â†’ TSet
homáµ’ t =
  tset
    (Î» t' â†’ t â‰¤ t')
    (Î» p q â†’ â‰¤-trans q p)
    (Î» p â†’ â‰¤-irrelevant _ _)
    (Î» p q r â†’ â‰¤-irrelevant _ _)

homá¶  : âˆ€ {t t'} â†’ t â‰¤ t' â†’ homáµ’ t' â†’áµ— homáµ’ t
homá¶  p =
  tset-map
    (Î» q â†’ â‰¤-trans p q)
    (Î» p q â†’ â‰¤-irrelevant _ _)

homá¶ -refl : âˆ€ {t} â†’ homá¶  (â‰¤-refl {t}) â‰¡áµ— idáµ—
homá¶ -refl p = â‰¤-irrelevant _ _

homá¶ -trans : âˆ€ {t t' t''}
           â†’ (p : t â‰¤ t') â†’ (q : t' â‰¤ t'')
           â†’ homá¶  p âˆ˜áµ— homá¶  q â‰¡áµ— homá¶  (â‰¤-trans p q)
homá¶ -trans p q r = â‰¤-irrelevant _ _

hom-iso-map : âˆ€ {A t} â†’ carrier A t â†’ homáµ’ t â†’áµ— A
hom-iso-map {A} x =
  tset-map
    (Î» p â†’ monotone A p x)
    (Î» p q â†’ sym (monotone-trans A q p x))

hom-iso-mapâ»Â¹ : âˆ€ {A t} â†’ homáµ’ t â†’áµ— A â†’ carrier A t
hom-iso-mapâ»Â¹ {A} f = map-carrier f â‰¤-refl

---- exponentials

_â‡’áµ—_ : TSet â†’ TSet â†’ TSet
A â‡’áµ— B =
  tset
    (Î» t â†’ homáµ’ t Ã—áµ— A â†’áµ— B)
    (Î» p f â†’ f âˆ˜áµ— mapË£áµ— (homá¶  p) idáµ—)
    (Î» {t} f â†’
      â‰¡áµ—-â‰¡ (Î» { (p , x) â†’
        cong (Î» q â†’ map-carrier f (q , x)) (â‰¤-irrelevant _ _) }))
    (Î» p q f â†’
      â‰¡áµ—-â‰¡ (Î» { (r , x) â†’
        cong (Î» s â†’ map-carrier f (s , x)) (â‰¤-irrelevant _ _) }))

infixr 22 _â‡’áµ—_

appáµ— : âˆ€ {A B} â†’ (A â‡’áµ— B) Ã—áµ— A â†’áµ— B
appáµ— {A} {B} =
  tset-map
    (Î» { (f , x) â†’ map-carrier f (â‰¤-refl , x) })
    (Î» { p (f , x) â†’
      trans
        (cong (Î» q â†’ map-carrier f (q , monotone A p x)) (â‰¤-irrelevant _ _))
        (map-nat f p (â‰¤-reflexive refl , x)) })

mapâ‡’áµ— : âˆ€ {A B C D} â†’ (A â†’áµ— B) â†’ (C â†’áµ— D) â†’ B â‡’áµ— C â†’áµ— A â‡’áµ— D
mapâ‡’áµ— f g =
  tset-map
    (Î» h â†’ g âˆ˜áµ— h âˆ˜áµ— mapË£áµ— idáµ— f)
    (Î» p h â†’ â‰¡áµ—-â‰¡ (Î» { (q , x) â†’ refl }))

curryáµ— : âˆ€ {A B C} â†’ A Ã—áµ— B â†’áµ— C â†’ A â†’áµ— B â‡’áµ— C
curryáµ— {A} f =
  tset-map
    (Î» x â†’ f âˆ˜áµ— mapË£áµ— (hom-iso-map x) idáµ—)
    (Î» p x â†’
      â‰¡áµ—-â‰¡ (Î» { (q , y) â†’
        cong
          (map-carrier f)
          (cong (_, y) (monotone-trans A p q x)) }))

uncurryáµ— : âˆ€ {A B C} â†’ A â†’áµ— B â‡’áµ— C â†’ A Ã—áµ— B â†’áµ— C
uncurryáµ— {A} {B} {C} f =
  tset-map
    (Î» { (x , y) â†’ map-carrier (map-carrier f x) (â‰¤-refl , y) })
    (Î» { p (x , y) â†’
      trans
        (cong
          (Î» z â†’ map-carrier z (â‰¤-reflexive refl , monotone B p y))
          (map-nat f p x))
        (trans
          (cong
            (Î» q â†’ map-carrier (map-carrier f x) (q , monotone B p y))
            (â‰¤-irrelevant _ _))
          (map-nat (map-carrier f x) p (â‰¤-reflexive refl , y))) })
