--------------------------------------------------------
-- Time-varying sets (covariant presheaves on (â„•,â‰¤)), --
-- their morphisms, and basic categorical structures  --
--------------------------------------------------------

module Semantics.TSets where

open import Function

open import Data.Empty
open import Data.Product renaming (map to mapË£)
open import Data.Sum renaming (map to mapâº)
open import Data.Unit hiding (_â‰¤_)

open import Util.Equality
open import Util.Time

-- Time-varying sets (covariant presheaves on (â„•,â‰¤))

record TSet : Setâ‚ where
  constructor
    tset
  field
    -- object map / carrier of the presheaf
    carrier  : Time â†’ Set
    -- functorial action / monotonicity on (â„•,â‰¤) of the presheaf
    monotone : âˆ€ {t t'} â†’ t â‰¤ t' â†’ carrier t â†’ carrier t'
    -- functorial action preserves identities and composition
    monotone-refl  : âˆ€ {t}
                   â†’ (x : carrier t) â†’ monotone (â‰¤-refl {t}) x â‰¡ x
    monotone-trans : âˆ€ {t t' t''} â†’ (p : t â‰¤ t') â†’ (q : t' â‰¤ t'')
                   â†’ (x : carrier t) â†’ monotone q (monotone p x) â‰¡ monotone (â‰¤-trans p q) x

open TSet public

-- Constant time-varying sets

ConstTSet : Set â†’ TSet
ConstTSet A = tset (Î» _ â†’ A) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)

-- Maps of time-varying sets

record _â†’áµ—_ (A B : TSet) : Set where
  constructor
    tset-map
  field
    -- carrier of a map between two presheaves
    map-carrier : âˆ€ {t} â†’ carrier A t â†’ carrier B t
    -- naturality of the map
    map-nat : âˆ€ {t t'} â†’ (p : t â‰¤ t')
            â†’ (x : carrier A t)
            â†’ map-carrier (monotone A p x) â‰¡ monotone B p (map-carrier x)

infix 20 _â†’áµ—_

open _â†’áµ—_ public

-- Equality of TSet-morphisms

record _â‰¡áµ—_ {A B : TSet} (f g : A â†’áµ— B) : Set where
  constructor
    eqáµ—
  field
    prf : âˆ€ {t} â†’ (x : carrier A t) â†’ map-carrier f x â‰¡ map-carrier g x

open _â‰¡áµ—_ public

infix 5 _â‰¡áµ—_

-- Reflexivity, symmetry, transitivity

abstract
  â‰¡áµ—-refl : âˆ€ {A B} {f : A â†’áµ— B} â†’ f â‰¡áµ— f
  â‰¡áµ—-refl = eqáµ— (Î» x â†’ refl)
   
  â‰¡áµ—-sym : âˆ€ {A B} {f g : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ g â‰¡áµ— f
  â‰¡áµ—-sym p = eqáµ— (Î» x â†’ sym (prf p x))
   
  â‰¡áµ—-trans : âˆ€ {A B} {f g h : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ g â‰¡áµ— h â†’ f â‰¡áµ— h
  â‰¡áµ—-trans p q = eqáµ— (Î» x â†’ trans (prf p x) (prf q x))

-- Begin-qed style reasoning for â‰¡áµ—

infix  3 _âˆ
infixr 2 _â‰¡âŸ¨âŸ©_ step-â‰¡
infix  1 begin_

begin_ : âˆ€ {A B} {f g : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ f â‰¡áµ— g
begin_ fâ‰¡g = fâ‰¡g

_â‰¡âŸ¨âŸ©_ : âˆ€ {A B} (f {g} : A â†’áµ— B) â†’ f â‰¡áµ— g â†’ f â‰¡áµ— g
_ â‰¡âŸ¨âŸ© fâ‰¡g = fâ‰¡g

step-â‰¡ : âˆ€ {A B} (f {g h} : A â†’áµ— B) â†’ g â‰¡áµ— h â†’ f â‰¡áµ— g â†’ f â‰¡áµ— h
step-â‰¡ _ gâ‰¡h fâ‰¡g = â‰¡áµ—-trans fâ‰¡g gâ‰¡h

_âˆ : âˆ€ {A B} (f : A â†’áµ— B) â†’ f â‰¡áµ— f
_âˆ _ = â‰¡áµ—-refl

syntax step-â‰¡ f gâ‰¡h fâ‰¡g = f â‰¡âŸ¨ fâ‰¡g âŸ© gâ‰¡h

-- â‰¡áµ— implies â‰¡ and vice versa

abstract
  â‰¡áµ—-â‰¡ : âˆ€ {A B} â†’ {f g : A â†’áµ— B} â†’ f â‰¡áµ— g â†’ f â‰¡ g
  â‰¡áµ—-â‰¡ p =
    dcongâ‚‚
      tset-map
        (ifun-ext (fun-ext (prf p)))
        (ifun-ext (ifun-ext (fun-ext (Î» q â†’ fun-ext (Î» x â†’ uip)))))

  â‰¡-â‰¡áµ— : âˆ€ {A B} â†’ {f g : A â†’áµ— B} â†’ f â‰¡ g â†’ f â‰¡áµ— g
  â‰¡-â‰¡áµ— refl = â‰¡áµ—-refl

-- Identity and composition of maps

abstract
  idáµ— : âˆ€ {A} â†’ A â†’áµ— A
  idáµ— = tset-map id (Î» p x â†’ refl)
   
  _âˆ˜áµ—_ : âˆ€ {A B C} â†’ B â†’áµ— C â†’ A â†’áµ— B â†’ A â†’áµ— C
  g âˆ˜áµ— f =
    tset-map
      (map-carrier g âˆ˜ map-carrier f)
      (Î» p x â†’
        trans
          (cong (Î» y â†’ map-carrier g y) (map-nat f p x))
          (map-nat g p (map-carrier f x)))

  idáµ—-reveal : âˆ€ {A}
             â†’ âˆ€ {t} â†’ (x : carrier A t) â†’ map-carrier (idáµ— {A}) x â‰¡ x
  idáµ—-reveal x = refl

  âˆ˜áµ—-reveal : âˆ€ {A B C} â†’ (g : B â†’áµ— C) â†’ (f : A â†’áµ— B)
            â†’ âˆ€ {t} â†’ (x : carrier A t) â†’ map-carrier (g âˆ˜áµ— f) x â‰¡ map-carrier g (map-carrier f x)
  âˆ˜áµ—-reveal g f x = refl

infixr 9 _âˆ˜áµ—_

-- Identity, associativity, and congruence laws

abstract
  âˆ˜áµ—-identityË¡ : âˆ€ {A B}
               â†’ (f : A â†’áµ— B)
               â†’ idáµ— âˆ˜áµ— f â‰¡áµ— f
  âˆ˜áµ—-identityË¡ f = eqáµ— (Î» x â†’ refl)
   
  âˆ˜áµ—-identityÊ³ : âˆ€ {A B}
               â†’ (f : A â†’áµ— B)
               â†’ f âˆ˜áµ— idáµ— â‰¡áµ— f
  âˆ˜áµ—-identityÊ³ f = eqáµ— (Î» x â†’ refl)
   
  âˆ˜áµ—-assoc : âˆ€ {A B C D}
           â†’ (h : C â†’áµ— D)
           â†’ (g : B â†’áµ— C)
           â†’ (f : A â†’áµ— B)
           â†’ (h âˆ˜áµ— g) âˆ˜áµ— f â‰¡áµ— h âˆ˜áµ— (g âˆ˜áµ— f)
  âˆ˜áµ—-assoc h g f = eqáµ— (Î» x â†’ refl)
   
  âˆ˜áµ—-congË¡ : âˆ€ {A B C}
           â†’ {f : A â†’áµ— B}
           â†’ {g h : B â†’áµ— C}
           â†’ g â‰¡áµ— h
           â†’ g âˆ˜áµ— f â‰¡áµ— h âˆ˜áµ— f
  âˆ˜áµ—-congË¡ {f = f} p =
    eqáµ— (Î» x â†’ cong-app (fun-ext (prf p)) (map-carrier f x))
   
  âˆ˜áµ—-congÊ³ : âˆ€ {A B C}
           â†’ {f g : A â†’áµ— B}
           â†’ {h : B â†’áµ— C}
           â†’ f â‰¡áµ— g
           â†’ h âˆ˜áµ— f â‰¡áµ— h âˆ˜áµ— g
  âˆ˜áµ—-congÊ³ {h = h} p =
    eqáµ— (Î» x â†’ cong (map-carrier h) (prf p x))


-- Product, sum, exponent, etc structures

---- terminal object

abstract
  ğŸ™áµ— : TSet
  ğŸ™áµ— = tset (Î» _ â†’ âŠ¤) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)
   
  terminaláµ— : âˆ€ {A} â†’ A â†’áµ— ğŸ™áµ—
  terminaláµ— = tset-map (Î» _ â†’ tt) (Î» p x â†’ refl)

  terminaláµ—-unique : âˆ€ {A} {f : A â†’áµ— ğŸ™áµ—}
                   â†’ f â‰¡áµ— terminaláµ—
  terminaláµ—-unique = eqáµ— (Î» x â†’ refl)


---- initial object

abstract
  ğŸ˜áµ— : TSet
  ğŸ˜áµ— = tset (Î» _ â†’ âŠ¥) (Î» _ â†’ id) (Î» _ â†’ refl) (Î» _ _ _ â†’ refl)
   
  initialáµ— : âˆ€ {A} â†’ ğŸ˜áµ— â†’áµ— A
  initialáµ— = tset-map (Î» ()) (Î» { p () })

  initialáµ—-unique : âˆ€ {A} {f : ğŸ˜áµ— â†’áµ— A}
                  â†’ f â‰¡áµ— initialáµ—
  initialáµ—-unique = eqáµ— (Î» ())


---- binary products

abstract
  _Ã—áµ—_ : TSet â†’ TSet â†’ TSet
  A Ã—áµ— B =
    tset
      (Î» t â†’ carrier A t Ã— carrier B t)
      (Î» p â†’ mapË£ (monotone A p) (monotone B p))
      (Î» x â†’ congâ‚‚ _,_ (monotone-refl A (projâ‚ x)) (monotone-refl B (projâ‚‚ x)))
      (Î» p q x â†’ congâ‚‚ _,_ (monotone-trans A p q (projâ‚ x)) (monotone-trans B p q (projâ‚‚ x)))

infixr 23 _Ã—áµ—_

abstract
  fstáµ— : âˆ€ {A B} â†’ A Ã—áµ— B â†’áµ— A
  fstáµ— = tset-map projâ‚ (Î» { p (x , y) â†’ refl })
   
  sndáµ— : âˆ€ {A B} â†’ A Ã—áµ— B â†’áµ— B
  sndáµ— = tset-map projâ‚‚ (Î» { p (x , y) â†’ refl })
   
  âŸ¨_,_âŸ©áµ— : âˆ€ {A B C} â†’ A â†’áµ— B â†’ A â†’áµ— C â†’ A â†’áµ— B Ã—áµ— C
  âŸ¨ f , g âŸ©áµ— =
    tset-map
      < map-carrier f , map-carrier g >
      (Î» p x â†’ congâ‚‚ _,_ (map-nat f p x) (map-nat g p x))

mapË£áµ— : âˆ€ {A B C D} â†’ A â†’áµ— C â†’ B â†’áµ— D â†’ A Ã—áµ— B â†’áµ— C Ã—áµ— D
mapË£áµ— f g = âŸ¨ f âˆ˜áµ— fstáµ— , g âˆ˜áµ— sndáµ— âŸ©áµ—
 
Ã—áµ—-assoc : âˆ€ {A B C} â†’ A Ã—áµ— (B Ã—áµ— C) â†’áµ— (A Ã—áµ— B) Ã—áµ— C
Ã—áµ—-assoc = âŸ¨ âŸ¨ fstáµ— , fstáµ— âˆ˜áµ— sndáµ— âŸ©áµ— , sndáµ— âˆ˜áµ— sndáµ— âŸ©áµ—
 
Ã—áµ—-assocâ»Â¹ : âˆ€ {A B C} â†’ (A Ã—áµ— B) Ã—áµ— C â†’áµ— A Ã—áµ— (B Ã—áµ— C)
Ã—áµ—-assocâ»Â¹ = âŸ¨ fstáµ— âˆ˜áµ— fstáµ— , âŸ¨ sndáµ— âˆ˜áµ— fstáµ— , sndáµ— âŸ©áµ— âŸ©áµ—

Ã—áµ—-swap : âˆ€ {A B} â†’ A Ã—áµ— B â†’áµ— B Ã—áµ— A
Ã—áµ—-swap = âŸ¨ sndáµ— , fstáµ— âŸ©áµ—

abstract
  âŸ¨âŸ©áµ—-fstáµ— : âˆ€ {A B C}
           â†’ (f : A â†’áµ— B)
           â†’ (g : A â†’áµ— C)
           â†’ fstáµ— âˆ˜áµ— âŸ¨ f , g âŸ©áµ— â‰¡áµ— f
  âŸ¨âŸ©áµ—-fstáµ— f g = eqáµ— (Î» x â†’ refl)
   
  âŸ¨âŸ©áµ—-sndáµ— : âˆ€ {A B C}
           â†’ (f : A â†’áµ— B)
           â†’ (g : A â†’áµ— C)
           â†’ sndáµ— âˆ˜áµ— âŸ¨ f , g âŸ©áµ— â‰¡áµ— g
  âŸ¨âŸ©áµ—-sndáµ— f g = eqáµ— (Î» x â†’ refl)

  âŸ¨âŸ©áµ—-unique : âˆ€ {A B C} â†’ (f : A â†’áµ— B) â†’ (g : A â†’áµ— C) â†’ (h : A â†’áµ— B Ã—áµ— C)
             â†’ fstáµ— âˆ˜áµ— h â‰¡áµ— f â†’ sndáµ— âˆ˜áµ— h â‰¡áµ— g
             â†’ h â‰¡áµ— âŸ¨ f , g âŸ©áµ—
  âŸ¨âŸ©áµ—-unique f g h (eqáµ— p) (eqáµ— q) =
    eqáµ— (Î» x â†’ congâ‚‚ _,_ (p x) (q x))

  âŸ¨âŸ©áµ—-âˆ˜áµ— : âˆ€ {A B C D} â†’ (f : A â†’áµ— B) â†’ (g : B â†’áµ— C) â†’ (h : B â†’áµ— D)
         â†’ âŸ¨ g âˆ˜áµ— f , h âˆ˜áµ— f âŸ©áµ— â‰¡áµ— âŸ¨ g , h âŸ©áµ— âˆ˜áµ— f
  âŸ¨âŸ©áµ—-âˆ˜áµ— f g h = 
    begin
      âŸ¨ g âˆ˜áµ— f , h âˆ˜áµ— f âŸ©áµ—
    â‰¡âŸ¨ â‰¡áµ—-sym
         (âŸ¨âŸ©áµ—-unique
           (g âˆ˜áµ— f) (h âˆ˜áµ— f) (âŸ¨ g , h âŸ©áµ— âˆ˜áµ— f)
           (begin
              fstáµ— âˆ˜áµ— âŸ¨ g , h âŸ©áµ— âˆ˜áµ— f
            â‰¡âŸ¨ â‰¡áµ—-sym (âˆ˜áµ—-assoc fstáµ— âŸ¨ g , h âŸ©áµ— f) âŸ©
              (fstáµ— âˆ˜áµ— âŸ¨ g , h âŸ©áµ—) âˆ˜áµ— f
            â‰¡âŸ¨ âˆ˜áµ—-congË¡ (âŸ¨âŸ©áµ—-fstáµ— g h) âŸ©
              g âˆ˜áµ— f
            âˆ)
           (begin
              sndáµ— âˆ˜áµ— âŸ¨ g , h âŸ©áµ— âˆ˜áµ— f
            â‰¡âŸ¨ â‰¡áµ—-sym (âˆ˜áµ—-assoc sndáµ— âŸ¨ g , h âŸ©áµ— f) âŸ©
              (sndáµ— âˆ˜áµ— âŸ¨ g , h âŸ©áµ—) âˆ˜áµ— f
            â‰¡âŸ¨ âˆ˜áµ—-congË¡ (âŸ¨âŸ©áµ—-sndáµ— g h) âŸ©
              h âˆ˜áµ— f
            âˆ))
     âŸ©
      âŸ¨ g , h âŸ©áµ— âˆ˜áµ— f
    âˆ

------ packing and unpacking the abstract definitions

abstract
  pack-Ã—áµ— : âˆ€ {A B t}
          â†’ carrier A t Ã— carrier B t
          â†’ carrier (A Ã—áµ— B) t
  pack-Ã—áµ— xy = xy

  unpack-Ã—áµ— : âˆ€ {A B t}
            â†’ carrier (A Ã—áµ— B) t
            â†’ carrier A t Ã— carrier B t
  unpack-Ã—áµ— xy = xy

  pack-unpack-Ã—áµ— : âˆ€ {A B t}
                 â†’ (xy : carrier A t Ã— carrier B t)
                 â†’ unpack-Ã—áµ— {A} {B} {t} (pack-Ã—áµ— xy) â‰¡ xy
  pack-unpack-Ã—áµ— xy = refl

  unpack-pack-Ã—áµ— : âˆ€ {A B t}
                 â†’ (xy : carrier (A Ã—áµ— B) t)
                 â†’ pack-Ã—áµ— {A} {B} {t} (unpack-Ã—áµ— xy) â‰¡ xy
  unpack-pack-Ã—áµ— xy = refl

  pack-Ã—áµ—-monotone : âˆ€ {A B t t'}
                   â†’ (p : t â‰¤ t')
                   â†’ (xy : carrier A t Ã— carrier B t)
                   â†’ monotone (A Ã—áµ— B) p (pack-Ã—áµ— xy)
                   â‰¡ pack-Ã—áµ— (monotone A p (projâ‚ xy) , monotone B p (projâ‚‚ xy))
  pack-Ã—áµ—-monotone p xy = refl

  unpack-Ã—áµ—-monotone : âˆ€ {A B t t'}
                     â†’ (p : t â‰¤ t')
                     â†’ (xy : carrier (A Ã—áµ— B) t)
                     â†’ (monotone A p (projâ‚ (unpack-Ã—áµ— xy)) , monotone B p (projâ‚‚ (unpack-Ã—áµ— xy)))
                     â‰¡ unpack-Ã—áµ— (monotone (A Ã—áµ— B) p xy)
  unpack-Ã—áµ—-monotone p xy = refl

  fstáµ—-reveal : âˆ€ {A B t}
              â†’ (xy : carrier (A Ã—áµ— B) t)
              â†’ map-carrier fstáµ— xy
              â‰¡ projâ‚ (unpack-Ã—áµ— xy)
  fstáµ—-reveal xy = refl

  sndáµ—-reveal : âˆ€ {A B t}
              â†’ (xy : carrier (A Ã—áµ— B) t)
              â†’ map-carrier sndáµ— xy
              â‰¡ projâ‚‚ (unpack-Ã—áµ— xy)
  sndáµ—-reveal xy = refl

  âŸ¨âŸ©áµ—-reveal : âˆ€ {A B C t}
             â†’ (f : A â†’áµ— B)
             â†’ (g : A â†’áµ— C)
             â†’ (x : carrier A t)
             â†’ map-carrier âŸ¨ f , g âŸ©áµ— x
             â‰¡ pack-Ã—áµ— (map-carrier f x , map-carrier g x)
  âŸ¨âŸ©áµ—-reveal f g x = refl


---- Set-indexed products

abstract
  Î  : (I : Set) â†’ (I â†’ TSet) â†’ TSet
  Î  I A =
    tset
      (Î» Ï„ â†’ (i : I) â†’ carrier (A i) Ï„)
      (Î» p f i â†’ monotone (A i) p (f i))
      (Î» f â†’ fun-ext (Î» i â†’ monotone-refl (A i) (f i)))
      (Î» p q f â†’ fun-ext (Î» i â†’ monotone-trans (A i) p q (f i)))
   
  projáµ— : âˆ€ {I A} â†’ (i : I) â†’ Î  I A â†’áµ— A i
  projáµ— i =
    tset-map
      (Î» f â†’ f i)
      (Î» p f â†’ refl)
      
  âŸ¨_âŸ©áµ¢áµ— : âˆ€ {A I B} â†’ ((i : I) â†’ A â†’áµ— B i) â†’ A â†’áµ— Î  I B
  âŸ¨ fs âŸ©áµ¢áµ— =
    tset-map
      (Î» x i â†’ map-carrier (fs i) x)
      (Î» p x â†’ fun-ext (Î» i â†’ map-nat (fs i) p x))
   
mapâ±Ë£áµ— : âˆ€ {I A B} â†’ ((i : I) â†’ A i â†’áµ— B i) â†’ Î  I A â†’áµ— Î  I B
mapâ±Ë£áµ— fs = âŸ¨ (Î» i â†’ fs i âˆ˜áµ— projáµ— i) âŸ©áµ¢áµ—


---- covariant hom-functor

abstract
  homáµ’ : Time â†’ TSet
  homáµ’ t =
    tset
      (Î» t' â†’ t â‰¤ t')
      (Î» p q â†’ â‰¤-trans q p)
      (Î» p â†’ â‰¤-irrelevant _ _)
      (Î» p q r â†’ â‰¤-irrelevant _ _)

  homá¶  : âˆ€ {t t'} â†’ t â‰¤ t' â†’ homáµ’ t' â†’áµ— homáµ’ t
  homá¶  p =
    tset-map
      (Î» q â†’ â‰¤-trans p q)
      (Î» p q â†’ â‰¤-irrelevant _ _)
   
  homá¶ -refl : âˆ€ {t} â†’ homá¶  (â‰¤-refl {t}) â‰¡áµ— idáµ—
  homá¶ -refl = eqáµ— Î» p â†’ â‰¤-irrelevant _ _
   
  homá¶ -trans : âˆ€ {t t' t''}
             â†’ (p : t â‰¤ t') â†’ (q : t' â‰¤ t'')
             â†’ homá¶  p âˆ˜áµ— homá¶  q â‰¡áµ— homá¶  (â‰¤-trans p q)
  homá¶ -trans p q = eqáµ— (Î» r â†’ â‰¤-irrelevant _ _)
   
  hom-iso-map : âˆ€ {A t} â†’ carrier A t â†’ homáµ’ t â†’áµ— A
  hom-iso-map {A} x =
    tset-map
      (Î» p â†’ monotone A p x)
      (Î» p q â†’ sym (monotone-trans A q p x))
   
  hom-iso-mapâ»Â¹ : âˆ€ {A t} â†’ homáµ’ t â†’áµ— A â†’ carrier A t
  hom-iso-mapâ»Â¹ {A} f = map-carrier f â‰¤-refl

------ packing and unpacking the abstract definitions

abstract
  pack-homáµ’ : âˆ€ {t'} (t : Time)
            â†’ t â‰¤ t'
            â†’ carrier (homáµ’ t) t'
  pack-homáµ’ t p = p

  unpack-homáµ’ : âˆ€ {t'} (t : Time)
              â†’ carrier (homáµ’ t) t'
              â†’ t â‰¤ t'
  unpack-homáµ’ t p = p

  pack-homáµ’-monotone : âˆ€ {t t' t''}
                     â†’ (p : t' â‰¤ t'')
                     â†’ (q : t â‰¤ t')
                     â†’ monotone (homáµ’ t) p (pack-homáµ’ t q)
                     â‰¡ pack-homáµ’ t (â‰¤-trans q p)
  pack-homáµ’-monotone p q = refl

  unpack-homáµ’-monotone : âˆ€ {t t' t''}
                       â†’ (p : t' â‰¤ t'')
                       â†’ (q : carrier (homáµ’ t) t')
                       â†’ â‰¤-trans (unpack-homáµ’ t q) p
                       â‰¡ unpack-homáµ’ t (monotone (homáµ’ t) p q)
  unpack-homáµ’-monotone p q = refl


---- exponentials

abstract
  _â‡’áµ—_ : TSet â†’ TSet â†’ TSet
  A â‡’áµ— B =
    tset
      (Î» t â†’ homáµ’ t Ã—áµ— A â†’áµ— B)
      (Î» p f â†’ f âˆ˜áµ— mapË£áµ— (homá¶  p) idáµ—)
      (Î» {t} f â†’
        â‰¡áµ—-â‰¡ (eqáµ— (Î» { (p , x) â†’
          cong (Î» q â†’ map-carrier f (q , x)) (â‰¤-irrelevant _ _) })))
      (Î» p q f â†’
        â‰¡áµ—-â‰¡ (eqáµ— (Î» { (r , x) â†’
          cong (Î» s â†’ map-carrier f (s , x)) (â‰¤-irrelevant _ _) })))

infixr 22 _â‡’áµ—_

abstract
  appáµ— : âˆ€ {A B} â†’ (A â‡’áµ— B) Ã—áµ— A â†’áµ— B
  appáµ— {A} {B} =
    tset-map
      (Î» { (f , x) â†’ map-carrier f (â‰¤-refl , x) })
      (Î» { p (f , x) â†’
        trans
          (cong (Î» q â†’ map-carrier f (q , monotone A p x)) (â‰¤-irrelevant _ _))
          (map-nat f p (â‰¤-reflexive refl , x)) })
   
  mapâ‡’áµ— : âˆ€ {A B C D} â†’ (A â†’áµ— B) â†’ (C â†’áµ— D) â†’ B â‡’áµ— C â†’áµ— A â‡’áµ— D
  mapâ‡’áµ— f g =
    tset-map
      (Î» h â†’ g âˆ˜áµ— h âˆ˜áµ— mapË£áµ— idáµ— f)
      (Î» p h â†’ â‰¡áµ—-â‰¡ (eqáµ— (Î» { (q , x) â†’ refl })))

  curryáµ— : âˆ€ {A B C} â†’ A Ã—áµ— B â†’áµ— C â†’ A â†’áµ— B â‡’áµ— C
  curryáµ— {A} f =
    tset-map
      (Î» x â†’ f âˆ˜áµ— mapË£áµ— (hom-iso-map x) idáµ—)
      (Î» p x â†’
        â‰¡áµ—-â‰¡ (eqáµ— (Î» { (q , y) â†’
          cong
            (map-carrier f)
            (cong (_, y) (monotone-trans A p q x)) })))
   
  uncurryáµ— : âˆ€ {A B C} â†’ A â†’áµ— B â‡’áµ— C â†’ A Ã—áµ— B â†’áµ— C
  uncurryáµ— {A} {B} {C} f =
    tset-map
      (Î» { (x , y) â†’ map-carrier (map-carrier f x) (â‰¤-refl , y) })
      (Î» { p (x , y) â†’
        trans
          (cong
            (Î» z â†’ map-carrier z (â‰¤-reflexive refl , monotone B p y))
            (map-nat f p x))
          (trans
            (cong
              (Î» q â†’ map-carrier (map-carrier f x) (q , monotone B p y))
              (â‰¤-irrelevant _ _))
            (map-nat (map-carrier f x) p (â‰¤-reflexive refl , y))) })

  mapâ‡’áµ—-identity : âˆ€ {A B} â†’ mapâ‡’áµ— {A} {A} {B} {B} idáµ— idáµ— â‰¡áµ— idáµ—
  mapâ‡’áµ—-identity = eqáµ— (Î» f â†’ â‰¡áµ—-â‰¡ (eqáµ— (Î» x â†’ refl)))

  curryáµ—-mapË£áµ— : âˆ€ {A B C D E}
               â†’ (f : C Ã—áµ— D â†’áµ— E) â†’ (g : A â†’áµ— C) â†’ (h : B â†’áµ— D)
               â†’ curryáµ— (f âˆ˜áµ— mapË£áµ— g h) â‰¡áµ— mapâ‡’áµ— h idáµ— âˆ˜áµ— curryáµ— f âˆ˜áµ— g
  curryáµ—-mapË£áµ— f g h =
    eqáµ— (Î» x â†’
      â‰¡áµ—-â‰¡ (eqáµ— (Î» y â†’
        cong (map-carrier f)
          (congâ‚‚ _,_ (map-nat g _ x) refl))))

------ packing and unpacking the abstract definitions

abstract
  pack-â‡’áµ— : âˆ€ {A B t}
          â†’ homáµ’ t Ã—áµ— A â†’áµ— B
          â†’ carrier (A â‡’áµ— B) t
  pack-â‡’áµ— f = f

  unpack-â‡’áµ— : âˆ€ {A B t}
            â†’ carrier (A â‡’áµ— B) t
            â†’ homáµ’ t Ã—áµ— A â†’áµ— B
  unpack-â‡’áµ— f = f

  pack-unpack-â‡’áµ— : âˆ€ {A B t}
                 â†’ (f : homáµ’ t Ã—áµ— A â†’áµ— B)
                 â†’ unpack-â‡’áµ— {A} {B} {t} (pack-â‡’áµ— f) â‰¡ f
  pack-unpack-â‡’áµ— xy = refl

  pack-â‡’áµ—-monotone : âˆ€ {A B t t'}
                   â†’ (p : t â‰¤ t')
                   â†’ (f : homáµ’ t Ã—áµ— A â†’áµ— B)
                   â†’ monotone (A â‡’áµ— B) p (pack-â‡’áµ— f)
                   â‰¡ pack-â‡’áµ— {A} {B} {t'}
                       (tset-map
                         (Î» qv â†’
                           map-carrier f
                             (pack-Ã—áµ—
                               (pack-homáµ’ t (â‰¤-trans p (unpack-homáµ’ t' (projâ‚ (unpack-Ã—áµ— qv)))) ,
                                projâ‚‚ (unpack-Ã—áµ— qv))))
                         (Î» q rv â†’
                           trans
                             (cong (map-carrier f)
                               (trans
                                 (cong pack-Ã—áµ—
                                   (congâ‚‚ _,_
                                     (trans
                                       (cong (pack-homáµ’ t)
                                         (â‰¤-irrelevant _ _))
                                       (sym
                                         (pack-homáµ’-monotone _ _)))
                                     (sym (cong projâ‚‚ (unpack-Ã—áµ—-monotone q rv)))))
                                 (sym (pack-Ã—áµ—-monotone _ _))))
                             (map-nat f _ _)))
  pack-â‡’áµ—-monotone p f =
    cong (tset-map _) (ifun-ext (ifun-ext (fun-ext (Î» q â†’ fun-ext (Î» rv â†’ uip)))))

  unpack-â‡’áµ—-map-carrier : âˆ€ {A B t t' t''}
                        â†’ (f : carrier (A â‡’áµ— B) t)
                        â†’ (p : t â‰¤ t')
                        â†’ (q : t' â‰¤ t'')
                        â†’ (x : carrier A t'')
                        â†’ map-carrier (unpack-â‡’áµ— (monotone (A â‡’áµ— B) p f)) (pack-Ã—áµ— (pack-homáµ’ t' q , x))
                        â‰¡ map-carrier (unpack-â‡’áµ— f) (pack-Ã—áµ— (pack-homáµ’ t (â‰¤-trans p q) , x))
  unpack-â‡’áµ—-map-carrier f p q x = refl

  mapâ‡’áµ—-reveal : âˆ€ {A B C D t}
               â†’ (f : A â†’áµ— B)
               â†’ (g : C â†’áµ— D)
               â†’ (h : carrier (B â‡’áµ— C) t)
               â†’ map-carrier (mapâ‡’áµ— f g) h
               â‰¡ pack-â‡’áµ—
                   (g âˆ˜áµ— unpack-â‡’áµ— h âˆ˜áµ— mapË£áµ— idáµ— f)
  mapâ‡’áµ—-reveal f g h = refl
