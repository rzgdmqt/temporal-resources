---------------------------------------------------------
-- Free graded monad generated by algebraic operations --
---------------------------------------------------------

-- Note: A version of the monad that is not quotioned by
--       the delay equations (identity and composition)

module Semantics.Monad.Core where

open import Function

open import Data.Empty
open import Data.Product
open import Data.Unit hiding (_â‰¤_)

open import Semantics.TSets
open import Semantics.Modality.Future
open import Semantics.Modality.Past

open import Util.Equality
open import Util.Operations
open import Util.Time

-- Interpretation of ground types
---------------------------------

âŸ¦_âŸ§áµ : GType â†’ TSet
âŸ¦ Base B âŸ§áµ   = ConstTSet (BaseSet B)
âŸ¦ Unit âŸ§áµ     = ğŸ™áµ—
âŸ¦ Empty âŸ§áµ    = ğŸ˜áµ—
âŸ¦ [ Ï„ ]áµ A âŸ§áµ = [ Ï„ ]áµ’ âŸ¦ A âŸ§áµ


-- The free graded monad generated by the operations in Op
----------------------------------------------------------

-- Object mapping and time-monotonicity

mutual

  data TË¢ (A : TSet) : (Ï„ : Time) â†’ (t : Time) â†’ Set where

    leaf  : âˆ€ {t}
          â†’ carrier A t
          â†’ TË¢ A 0 t
       
    op-node  : âˆ€ {Ï„ t}
             â†’ (op : Op)
             â†’ carrier âŸ¦ param op âŸ§áµ t
             â†’ (k : {t' : Time} â†’ t + op-time op â‰¤ t'
                                â†’ carrier âŸ¦ arity op âŸ§áµ t'
                                â†’ TË¢ A Ï„ t')
             â†’ ({t' t'' : Time} â†’ (p : t' â‰¤ t'')                                       -- time-naturality of continuation
                                â†’ (q : t + op-time op â‰¤ t')
                                â†’ (y : carrier âŸ¦ arity op âŸ§áµ t')
                                â†’ k (â‰¤-trans q p) (monotone (âŸ¦ arity op âŸ§áµ) p y)
                                â‰¡ TË¢-â‰¤t p (k q y))
             â†’ TË¢ A (op-time op + Ï„) t
     
    delay-node : âˆ€ {Ï„' t}
               â†’ (Ï„ : Time)
               â†’ TË¢ A Ï„' (t + Ï„)
               â†’ TË¢ A (Ï„ + Ï„') t


  TË¢-â‰¤t : âˆ€ {A Ï„ t t'} â†’ t â‰¤ t' â†’ TË¢ A Ï„ t â†’ TË¢ A Ï„ t'
  TË¢-â‰¤t {A} p (leaf v) =
    leaf (monotone A p v)
  TË¢-â‰¤t p (op-node op v k k-nat) =
    op-node op
      (monotone (âŸ¦ param op âŸ§áµ) p v)
      (Î» q y â†’ k (â‰¤-trans (+-monoË¡-â‰¤ (op-time op) p) q) y)
      (Î» p q y â†’
        trans
          (cong (Î» q â†’ k q (monotone âŸ¦ arity op âŸ§áµ p y)) (â‰¤-irrelevant _ _))
          (k-nat _ _ y))
  TË¢-â‰¤t p (delay-node Ï„ k) =
    delay-node Ï„ (TË¢-â‰¤t (+-monoË¡-â‰¤ Ï„ p) k)


-- Reflexivity and transitivity of time-monotonicity

TË¢-â‰¤t-refl : âˆ€ {A Ï„ t} â†’ (c : TË¢ A Ï„ t) â†’ TË¢-â‰¤t â‰¤-refl c â‰¡ c
TË¢-â‰¤t-refl {A} (leaf v) =
  cong leaf (monotone-refl A v)
TË¢-â‰¤t-refl {A = A} {t = t} (op-node {Ï„ = Ï„} op v k k-nat) =
  dcongâ‚ƒ (op-node op)
    (monotone-refl âŸ¦ param op âŸ§áµ v)
    (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» y â†’
      trans
        (cong (Î» (k : {t' : Time} â†’ t + op-time op â‰¤ t' â†’ carrier âŸ¦ arity op âŸ§áµ t' â†’ TË¢ A Ï„ t') â†’ k p y)
          {subst (Î» _ â†’ {t' : Time} â†’ t + op-time op â‰¤ t' â†’ carrier âŸ¦ arity op âŸ§áµ t' â†’ TË¢ A Ï„ t')
              (monotone-refl âŸ¦ param op âŸ§áµ v)
              (Î» q â†’ k (â‰¤-trans (+-monoË¡-â‰¤ (op-time op) â‰¤-refl) q))}
          (subst-const _ (monotone-refl âŸ¦ param op âŸ§áµ v) _))
        (cong (Î» p â†’ k p y) (â‰¤-irrelevant _ _))))))
    (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))
TË¢-â‰¤t-refl (delay-node Ï„ k) =
  cong (delay-node Ï„)
    (trans
      (cong (Î» p â†’ TË¢-â‰¤t p k) (â‰¤-irrelevant _ _))
      (TË¢-â‰¤t-refl k))

TË¢-â‰¤t-trans : âˆ€ {A Ï„ t t' t''}
            â†’ (p : t â‰¤ t') â†’ (q : t' â‰¤ t'') â†’ (c : TË¢ A Ï„ t)
            â†’ TË¢-â‰¤t q (TË¢-â‰¤t p c) â‰¡ TË¢-â‰¤t (â‰¤-trans p q) c
TË¢-â‰¤t-trans {A} p q (leaf v) =
  cong leaf (monotone-trans A p q v)
TË¢-â‰¤t-trans {A = A} {t'' = t''} p q (op-node {Ï„ = Ï„} op v k k-nat) =
  dcongâ‚ƒ (op-node op)
    (monotone-trans (âŸ¦ param op âŸ§áµ) p q v)
    (ifun-ext (fun-ext (Î» r â†’ fun-ext (Î» y â†’
      trans
        (cong (Î» (k : {t''' : Time} â†’ t'' + op-time op â‰¤ t''' â†’ carrier âŸ¦ arity op âŸ§áµ t''' â†’ TË¢ A Ï„ t''') â†’ k r y)
          {subst
            (Î» _ â†’ {t' = t''' : Time} â†’ t'' + op-time op â‰¤ t''' â†’ carrier âŸ¦ arity op âŸ§áµ t''' â†’ TË¢ A Ï„ t''')
            (monotone-trans âŸ¦ param op âŸ§áµ p q v)
            (Î» r â†’ k (â‰¤-trans (+-monoË¡-â‰¤ (op-time op) p) (â‰¤-trans (+-monoË¡-â‰¤ (op-time op) q) r)))}
          (subst-const _ (monotone-trans âŸ¦ param op âŸ§áµ p q v) _))
        (cong (Î» p â†’ k p y) (â‰¤-irrelevant _ _))))))
    (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))
TË¢-â‰¤t-trans p q (delay-node Ï„ k) =
  cong (delay-node Ï„)
    (trans
      (TË¢-â‰¤t-trans (+-monoË¡-â‰¤ Ï„ p) (+-monoË¡-â‰¤ Ï„ q) k)
      (cong (Î» p â†’ TË¢-â‰¤t p k) (â‰¤-irrelevant _ _)))


-- Functorial action

mutual

  {-# TERMINATING #-}

  -- For now, telling Agda manually that the definitions below are
  -- terminating. The problem lies in Agda not seeing that uses of
  -- `TË¢á¶ ` in the types involved in the `op-node op ...` case are indeed
  -- applied to smaller arguments when calling the simultaneously
  -- defined function `TË¢á¶ -â‰¤t-nat` to fill a hole of type
  --
  --   `TË¢á¶  f (TË¢-â‰¤t p (k q y)) â‰¡ TË¢-â‰¤t p (TË¢á¶  f (k q y))`
  --
  -- Here `TË¢-â‰¤t` is masking that `k q y` and thus `TË¢-â‰¤t p (k q y)` 
  -- are in fact smaller trees.
  --
  -- Possible solutions include additionally indexing the trees by
  -- their heights, using sized types (when they become consistent),
  -- or moving the `k-nat` condition into a separate extrinsic
  -- predicate. The latter involves duplicating all definitions.
  --
  -- For now not implementing any of those workarounds to keep the
  -- presheaf model clean and not polluted by formalisation artefacts.
  -- 
  -- The other uses of {-# TERMINATING #-} in this and related `Monad`
  -- modules are included because of analogous reasons.

  TË¢á¶  : âˆ€ {A B Ï„} â†’ A â†’áµ— B â†’ {t : Time} â†’ TË¢ A Ï„ t â†’ TË¢ B Ï„ t
  TË¢á¶  f (leaf v) =
    leaf (map-carrier f v)
  TË¢á¶  f (op-node op v k k-nat) =
    op-node op v
      (Î» p y â†’ TË¢á¶  f (k p y))
      (Î» p q y â†’
        trans
          (cong (TË¢á¶  f) (k-nat p q y))
          (TË¢á¶ -â‰¤t-nat f p (k q y)))    
  TË¢á¶  f (delay-node Ï„ k) =
    delay-node Ï„ (TË¢á¶  f k)

  TË¢á¶ -â‰¤t-nat : âˆ€ {A B Ï„} â†’ (f : A â†’áµ— B) â†’ {t t' : â„•}
             â†’ (p : t â‰¤ t') â†’ (c : TË¢ A Ï„ t)
             â†’ TË¢á¶  f (TË¢-â‰¤t p c) â‰¡ TË¢-â‰¤t p (TË¢á¶  f c)
  TË¢á¶ -â‰¤t-nat f p (leaf v) =
    cong leaf (map-nat f p v)
  TË¢á¶ -â‰¤t-nat f p (op-node op v k k-nat) =
    dcongâ‚ƒ (op-node op)
      refl
      refl
      (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))
  TË¢á¶ -â‰¤t-nat f p (delay-node Ï„ k) =
    cong (delay-node Ï„) (TË¢á¶ -â‰¤t-nat f (+-monoË¡-â‰¤ Ï„ p) k)

TË¢á¶ -idáµ— : âˆ€ {A Ï„} â†’ {t : Time}
        â†’ (c : TË¢ A Ï„ t)
        â†’ TË¢á¶  idáµ— c â‰¡ c
TË¢á¶ -idáµ— (leaf v) =
  cong leaf (idáµ—-reveal v)
TË¢á¶ -idáµ— (op-node op v k k-nat) =
  dcongâ‚‚ (op-node op v)
    (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» y â†’ TË¢á¶ -idáµ— (k p y)))))
    (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))
TË¢á¶ -idáµ— (delay-node Ï„ k) =
  cong (delay-node Ï„) (TË¢á¶ -idáµ— k)

TË¢á¶ -âˆ˜áµ— : âˆ€ {A B C Ï„}
       â†’ (g : B â†’áµ— C)
       â†’ (f : A â†’áµ— B)
       â†’ {t : Time}
       â†’ (c : TË¢ A Ï„ t)
       â†’ TË¢á¶  (g âˆ˜áµ— f) c â‰¡ TË¢á¶  g (TË¢á¶  f c)
TË¢á¶ -âˆ˜áµ— g f (leaf v) =
  cong leaf (âˆ˜áµ—-reveal g f v)
TË¢á¶ -âˆ˜áµ— g f (op-node op v k k-nat) =
  dcongâ‚‚ (op-node op v)
    (ifun-ext (fun-ext (Î» p â†’ (fun-ext (Î» y â†’ TË¢á¶ -âˆ˜áµ— g f (k p y))))))
    (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))
TË¢á¶ -âˆ˜áµ— g f (delay-node Ï„ k) =
  cong (delay-node Ï„) (TË¢á¶ -âˆ˜áµ— g f k)


-- Packaging it all up into a functor on TSet

Táµ’ : TSet â†’ Time â†’ TSet
Táµ’ A Ï„ = tset (TË¢ A Ï„) TË¢-â‰¤t TË¢-â‰¤t-refl TË¢-â‰¤t-trans
 
Tá¶  : âˆ€ {A B Ï„} â†’ A â†’áµ— B â†’ Táµ’ A Ï„ â†’áµ— Táµ’ B Ï„
Tá¶  f = tset-map (TË¢á¶  f) (TË¢á¶ -â‰¤t-nat f)

Tá¶ -idáµ— : âˆ€ {A Ï„}
       â†’ Tá¶  {A} {A} {Ï„} idáµ— â‰¡áµ— idáµ—
Tá¶ -idáµ— =
  eqáµ— (Î» c â†’
    trans
      (TË¢á¶ -idáµ— c)
      (sym (idáµ—-reveal c)))

Tá¶ -âˆ˜áµ— : âˆ€ {A B C Ï„}
      â†’ (g : B â†’áµ— C)
      â†’ (f : A â†’áµ— B)
      â†’ Tá¶  {A} {C} {Ï„} (g âˆ˜áµ— f) â‰¡áµ— Tá¶  g âˆ˜áµ— Tá¶  f
Tá¶ -âˆ˜áµ— g f =
  eqáµ— (Î» c â†’
    trans
      (TË¢á¶ -âˆ˜áµ— g f c)
      (sym (âˆ˜áµ—-reveal (Tá¶  g) (Tá¶  f) c)))


-- "subst" for time-gradings

Ï„-substË¢ : âˆ€ {A Ï„ Ï„'}
         â†’ Ï„ â‰¡ Ï„'
         â†’ {t : Time}
         â†’ TË¢ A Ï„ t
         â†’ TË¢ A Ï„' t
Ï„-substË¢ refl c = c

Ï„-substË¢-â‰¤t : âˆ€ {A Ï„ Ï„' t t'}
            â†’ (p : Ï„ â‰¡ Ï„')
            â†’ (q : t â‰¤ t')
            â†’ (c : TË¢ A Ï„ t)
            â†’ Ï„-substË¢ p (TË¢-â‰¤t q c) â‰¡ TË¢-â‰¤t q (Ï„-substË¢ p c)
Ï„-substË¢-â‰¤t refl q c = refl

Ï„-substË¢-trans : âˆ€ {A Ï„ Ï„' Ï„'' t}
               â†’ (p : Ï„ â‰¡ Ï„')
               â†’ (q : Ï„' â‰¡ Ï„'')
               â†’ (c : TË¢ A Ï„ t)
               â†’ Ï„-substË¢ q (Ï„-substË¢ p c) â‰¡ Ï„-substË¢ (trans p q) c
Ï„-substË¢-trans refl refl c = refl

Ï„-substáµ€ : âˆ€ {A Ï„ Ï„'}
         â†’ Ï„ â‰¡ Ï„'
         â†’ Táµ’ A Ï„ â†’áµ— Táµ’ A Ï„'
Ï„-substáµ€ p =
  tset-map
    (Ï„-substË¢ p)
    (Ï„-substË¢-â‰¤t p)

Ï„-substË¢-TË¢á¶  : âˆ€ {A B Ï„ Ï„' t}
             â†’ (p : Ï„ â‰¡ Ï„')
             â†’ (f : A â†’áµ— B)
             â†’ (c : TË¢ A Ï„ t)
             â†’ Ï„-substË¢ p (TË¢á¶  f c) â‰¡ TË¢á¶  f (Ï„-substË¢ p c)
Ï„-substË¢-TË¢á¶  refl f c = refl

Ï„-substË¢-delay : âˆ€ {A Ï„' Ï„'' t}
               â†’ (Ï„ : Time)
               â†’ (p : Ï„' â‰¡ Ï„'')
               â†’ (c : TË¢ A Ï„' (t + Ï„))
               â†’ Ï„-substË¢ (cong (Ï„ +_) p) (delay-node Ï„ c)
               â‰¡ delay-node Ï„ (Ï„-substË¢ p c)
Ï„-substË¢-delay Ï„ refl c = refl

Ï„-substË¢-op : âˆ€ {A Ï„ Ï„' t}
              â†’ (p : Ï„ â‰¡ Ï„')
              â†’ (op : Op)
              â†’ (v : carrier âŸ¦ param op âŸ§áµ t)
              â†’ (k : {t' : Time} â†’ t + op-time op â‰¤ t' â†’ carrier âŸ¦ arity op âŸ§áµ t' â†’ TË¢ A Ï„ t')
              â†’ (k-nat : {t' t'' : Time} (q : t' â‰¤ t'') (r : t + op-time op â‰¤ t')
                         (y : carrier âŸ¦ arity op âŸ§áµ t') â†’
                         k (â‰¤-trans r q) (monotone âŸ¦ arity op âŸ§áµ q y) â‰¡ TË¢-â‰¤t q (k r y))
              â†’ Ï„-substË¢
                  (cong (op-time op +_) p)
                  (op-node op v k k-nat)
              â‰¡ op-node op v
                  (Î» q y â†’
                    Ï„-substË¢ p (k q y))
                  (Î» q r y â†’
                    trans
                      (cong (Ï„-substË¢ p) (k-nat q r y))
                      (Ï„-substË¢-â‰¤t p q (k r y)))
Ï„-substË¢-op refl op v k k-nat =
  dcongâ‚‚ (op-node op v)
    refl
    (ifun-ext (ifun-ext (fun-ext (Î» q â†’ fun-ext (Î» r â†’ fun-ext (Î» y â†’ uip))))))


-- Unit

Î·áµ€ : âˆ€ {A} â†’ A â†’áµ— Táµ’ A 0
Î·áµ€ =
  tset-map
    (Î» v â†’ leaf v)
    (Î» p v â†’ refl)

Î·áµ€-nat : âˆ€ {A B}
       â†’ (f : A â†’áµ— B)
       â†’ Î·áµ€ âˆ˜áµ— f â‰¡áµ— Tá¶  f âˆ˜áµ— Î·áµ€
Î·áµ€-nat f =
  eqáµ— (Î» c â†’
    trans
      (âˆ˜áµ—-reveal Î·áµ€ f c)
      (sym (âˆ˜áµ—-reveal (Tá¶  f) Î·áµ€ c)))


-- Multiplication

mutual

  {-# TERMINATING #-}

  Î¼Ë¢ : âˆ€ {A Ï„ Ï„'} â†’ {t : Time}
     â†’ TË¢ (Táµ’ A Ï„') Ï„ t â†’ TË¢ A (Ï„ + Ï„') t
  Î¼Ë¢ (leaf c) =
    c
  Î¼Ë¢ (op-node op v k k-nat) =
    Ï„-substË¢
      (sym (+-assoc (op-time op) _ _))
      (op-node op v
        (Î» p y â†’ Î¼Ë¢ (k p y))
        (Î» p q y â†’
          trans
            (cong Î¼Ë¢ (k-nat p q y))
            (Î¼Ë¢-â‰¤t-nat p (k q y))))
  Î¼Ë¢ (delay-node Ï„ k) =
    Ï„-substË¢ (sym (+-assoc Ï„ _ _)) (delay-node Ï„ (Î¼Ë¢ k))

  Î¼Ë¢-â‰¤t-nat : âˆ€ {A Ï„ Ï„'} â†’ {t t' : â„•}
          â†’ (p : t â‰¤ t')
          â†’ (c : TË¢ (Táµ’ A Ï„') Ï„ t)
          â†’ Î¼Ë¢ (TË¢-â‰¤t p c) â‰¡ TË¢-â‰¤t p (Î¼Ë¢ c)
  Î¼Ë¢-â‰¤t-nat p (leaf v) =
    refl
  Î¼Ë¢-â‰¤t-nat p (op-node op v k k-nat) =
    trans
      (cong (Ï„-substË¢ (sym (+-assoc (op-time op) _ _)))
        (dcongâ‚‚ (op-node op (monotone âŸ¦ param op âŸ§áµ p v))
          refl
          (ifun-ext (ifun-ext (fun-ext (Î» q â†’ fun-ext (Î» r â†’ fun-ext (Î» y â†’ uip))))))))
      (Ï„-substË¢-â‰¤t (sym (+-assoc (op-time op) _ _)) p _)
  Î¼Ë¢-â‰¤t-nat p (delay-node Ï„ k) =
    trans
      (cong
        (Ï„-substË¢ (sym (+-assoc Ï„ _ _)))
        (cong (delay-node Ï„) (Î¼Ë¢-â‰¤t-nat (+-monoË¡-â‰¤ Ï„ p) k)))
      (Ï„-substË¢-â‰¤t (sym (+-assoc Ï„ _ _)) p (delay-node Ï„ (Î¼Ë¢ k)))

Î¼áµ€ : âˆ€ {A Ï„ Ï„'}
   â†’ Táµ’ (Táµ’ A Ï„') Ï„ â†’áµ— Táµ’ A (Ï„ + Ï„')
Î¼áµ€ = tset-map Î¼Ë¢ Î¼Ë¢-â‰¤t-nat

Î¼Ë¢-nat : âˆ€ {A B Ï„ Ï„'}
       â†’ (f : A â†’áµ— B)
       â†’ {t : Time}
       â†’ (c : TË¢ (Táµ’ A Ï„') Ï„ t)
       â†’ Î¼Ë¢ (TË¢á¶  (Tá¶  f) c) â‰¡ TË¢á¶  f (Î¼Ë¢ c)
Î¼Ë¢-nat f (leaf v) =
  refl
Î¼Ë¢-nat f (op-node op v k k-nat) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc (op-time op) _ _)))
      (dcongâ‚‚ (op-node op v)
        (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» y â†’ Î¼Ë¢-nat f (k p y)))))
        (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))))
    (Ï„-substË¢-TË¢á¶  (sym (+-assoc (op-time op) _ _)) f _)
Î¼Ë¢-nat f (delay-node Ï„ k) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc Ï„ _ _)))
      (cong (delay-node Ï„)
        (Î¼Ë¢-nat f k)))
    (Ï„-substË¢-TË¢á¶  (sym (+-assoc Ï„ _ _)) f (delay-node Ï„ (Î¼Ë¢ k)))

Î¼áµ€-nat : âˆ€ {A B Ï„ Ï„'}
       â†’ (f : A â†’áµ— B)
       â†’ Î¼áµ€ {Ï„ = Ï„} {Ï„' = Ï„'} âˆ˜áµ— Tá¶  (Tá¶  f) â‰¡áµ— Tá¶  f âˆ˜áµ— Î¼áµ€
Î¼áµ€-nat f =
  eqáµ— (Î» c â†’
    trans
      (âˆ˜áµ—-reveal Î¼áµ€ (Tá¶  (Tá¶  f)) c)
      (trans
        (Î¼Ë¢-nat f c)
        (sym (âˆ˜áµ—-reveal (Tá¶  f) Î¼áµ€ c))))

Ï„-substË¢-Î¼Ë¢ : âˆ€ {A Ï„ Ï„' Ï„'' t}
           â†’ (p : Ï„ â‰¡ Ï„'')
           â†’ (c : TË¢ (Táµ’ A Ï„') Ï„ t)
           â†’ Ï„-substË¢ (cong (_+ Ï„') p) (Î¼Ë¢ c)
           â‰¡ Î¼Ë¢ (Ï„-substË¢ p c)
Ï„-substË¢-Î¼Ë¢ refl c = refl


-- Monad laws

---- First identity law

Î¼áµ€-identityâ‚ : âˆ€ {A Ï„}
             â†’  Î¼áµ€ {Ï„ = 0} {Ï„' = Ï„} âˆ˜áµ— Î·áµ€ {Táµ’ A Ï„} â‰¡áµ— idáµ—
Î¼áµ€-identityâ‚ =
  eqáµ— (Î» c â†’
    trans (âˆ˜áµ—-reveal Î¼áµ€ Î·áµ€ c) (sym (idáµ—-reveal c)))

---- Second identity law

Î¼Ë¢-identityâ‚‚ : âˆ€ {A Ï„}
             â†’ {t : Time}
             â†’ (c : TË¢ A Ï„ t)
             â†’ Î¼Ë¢ {Ï„ = Ï„} {Ï„' = 0} (TË¢á¶  (Î·áµ€ {A}) c)
             â‰¡ Ï„-substË¢ (sym (+-identityÊ³ Ï„)) c
Î¼Ë¢-identityâ‚‚ (leaf v) =
  refl
Î¼Ë¢-identityâ‚‚ (op-node {Ï„} op v k k-nat) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc (op-time op) Ï„ 0)))
      (dcongâ‚‚ (op-node op v)
        {yâ‚‚ = Î» p q y â†’
          trans
            (cong (Ï„-substË¢ (sym (+-identityÊ³ Ï„)))
              (k-nat p q y))
            (Ï„-substË¢-â‰¤t _ _ (k q y))}
        (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» y â†’ Î¼Ë¢-identityâ‚‚ (k p y)))))
        (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))))
    (trans
      (cong (Ï„-substË¢ (sym (+-assoc (op-time op) Ï„ 0)))
        (sym (Ï„-substË¢-op _ op v k k-nat)))
      (trans
        (Ï„-substË¢-trans _ (sym (+-assoc (op-time op) Ï„ 0)) (op-node op v k k-nat))
        (cong (Î» p â†’ Ï„-substË¢ p (op-node op v k k-nat)) uip)))
Î¼Ë¢-identityâ‚‚ (delay-node {Ï„'} Ï„ k) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc Ï„ Ï„' 0)))
      (cong (delay-node Ï„)
        (Î¼Ë¢-identityâ‚‚ k)))
    (trans
      (cong (Ï„-substË¢ (sym (+-assoc Ï„ Ï„' 0)))
        (sym (Ï„-substË¢-delay Ï„ _ k)))
      (trans
        (Ï„-substË¢-trans _ (sym (+-assoc Ï„ Ï„' 0)) (delay-node Ï„ k))
        (cong (Î» p â†’ Ï„-substË¢ p (delay-node Ï„ k)) uip)))

Î¼áµ€-identityâ‚‚ : âˆ€ {A Ï„}
             â†’  Î¼áµ€ {Ï„ = Ï„} {Ï„' = 0} âˆ˜áµ— Tá¶  (Î·áµ€ {A})
             â‰¡áµ— Ï„-substáµ€ (sym (+-identityÊ³ Ï„))
Î¼áµ€-identityâ‚‚ =
  eqáµ— (Î» c â†’
    trans
      (âˆ˜áµ—-reveal Î¼áµ€ (Tá¶  Î·áµ€) c)
      (Î¼Ë¢-identityâ‚‚ c))

---- Associativity law

Î¼Ë¢-assoc : âˆ€ {A Ï„ Ï„' Ï„'' t}
         â†’ (c : carrier (Táµ’ (Táµ’ (Táµ’ A Ï„'') Ï„') Ï„) t)
         â†’ Î¼Ë¢ {A} {Ï„} {Ï„' + Ï„''} (TË¢á¶  Î¼áµ€ c)
         â‰¡ Ï„-substË¢ (+-assoc Ï„ Ï„' Ï„'') (Î¼Ë¢ (Î¼Ë¢ c))
Î¼Ë¢-assoc (leaf v) =
  refl
Î¼Ë¢-assoc {A} {Ï„' = Ï„'} {Ï„'' = Ï„''} (op-node {Ï„ = Ï„} op v k k-nat) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc (op-time op) Ï„ (Ï„' + Ï„''))))
      (dcongâ‚‚ (op-node op v)
        {yâ‚‚ = Î» p q y â†’
          trans
            (cong (Ï„-substË¢ (+-assoc Ï„ Ï„' Ï„''))
              (trans
                (cong (Î» c â†’ Î¼Ë¢ (Î¼Ë¢ c))
                  (k-nat p q y))
                (trans
                  (cong Î¼Ë¢ (Î¼Ë¢-â‰¤t-nat _ (k q y)))
                  (Î¼Ë¢-â‰¤t-nat _ (Î¼Ë¢ (k q y))))))
            (Ï„-substË¢-â‰¤t _ _ (Î¼Ë¢ (Î¼Ë¢ (k q y))))}
        (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» y â†’ Î¼Ë¢-assoc (k p y)))))
        (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))))
    (trans
      (trans
        (sym
          (cong (Ï„-substË¢ (sym (+-assoc (op-time op) Ï„ (Ï„' + Ï„''))))
            (Ï„-substË¢-op
              (+-assoc Ï„ Ï„' Ï„'')
              op
              v
              (Î» p y â†’ Î¼Ë¢ (Î¼Ë¢ (k p y)))
              (Î» p q y â†’
                trans
                  (cong (Î» c â†’ Î¼Ë¢ (Î¼Ë¢ c))
                    (k-nat p q y))
                  (trans
                    (cong Î¼Ë¢ (Î¼Ë¢-â‰¤t-nat _ (k q y)))
                    (Î¼Ë¢-â‰¤t-nat _ (Î¼Ë¢ (k q y))))))))
        (trans
          (Ï„-substË¢-trans
            (cong (_+_ (op-time op)) (+-assoc Ï„ Ï„' Ï„''))
            (sym (+-assoc (op-time op) Ï„ (Ï„' + Ï„''))) _)
          (trans
            (trans
              (congâ‚‚
                (Î» p c â†’ Ï„-substË¢
                  {Ï„ = op-time op + (Ï„ + Ï„' + Ï„'')}
                  {Ï„' = op-time op + Ï„ + (Ï„' + Ï„'')} p c)
                {trans (cong (_+_ (op-time op)) (+-assoc Ï„ Ï„' Ï„''))
                  (sym (+-assoc (op-time op) Ï„ (Ï„' + Ï„'')))}
                {trans (sym (+-assoc (op-time op) (Ï„ + Ï„') Ï„''))
                  (trans (cong (_+ Ï„'') (sym (+-assoc (op-time op) Ï„ Ï„')))
                    (+-assoc (op-time op + Ï„) Ï„' Ï„''))}
                uip
                (cong (op-node op v (Î» p y â†’ Î¼Ë¢ (Î¼Ë¢ (k p y))))
                  (ifun-ext (ifun-ext (fun-ext (Î» p â†’ fun-ext (Î» q â†’ fun-ext (Î» y â†’ uip))))))))
              (sym
                (Ï„-substË¢-trans
                  (sym (+-assoc (op-time op) (Ï„ + Ï„') Ï„''))
                  (trans (cong (_+ Ï„'') (sym (+-assoc (op-time op) Ï„ Ï„')))
                    (+-assoc (op-time op + Ï„) Ï„' Ï„''))
                  _)))
            (sym
              (Ï„-substË¢-trans
                (cong (_+ Ï„'') (sym (+-assoc (op-time op) Ï„ Ï„')))
                (+-assoc (op-time op + Ï„) Ï„' Ï„'') _)))))
      (cong (Ï„-substË¢ (+-assoc (op-time op + Ï„) Ï„' Ï„''))
        (Ï„-substË¢-Î¼Ë¢
          (sym (+-assoc (op-time op) Ï„ Ï„'))
          (op-node op v
            (Î» p y â†’ Î¼Ë¢ (k p y))
            (Î» p q y â†’ trans (cong Î¼Ë¢ (k-nat p q y)) (Î¼Ë¢-â‰¤t-nat p (k q y)))))))
Î¼Ë¢-assoc {A} {Ï„' = Ï„'} {Ï„'' = Ï„''} (delay-node {Ï„' = Ï„'''} Ï„ k) =
  trans
    (cong (Ï„-substË¢ (sym (+-assoc Ï„ Ï„''' (Ï„' + Ï„''))))
      (cong (delay-node Ï„)
        (Î¼Ë¢-assoc k)))
    (trans
      (cong (Ï„-substË¢ (sym (+-assoc Ï„ Ï„''' (Ï„' + Ï„''))))
        (sym (Ï„-substË¢-delay Ï„ _ (Î¼Ë¢ (Î¼Ë¢ k)))))
      (trans
        (Ï„-substË¢-trans _ (sym (+-assoc Ï„ Ï„''' (Ï„' + Ï„''))) (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k))))
        (trans
          {j =
            Ï„-substË¢
              (+-assoc (Ï„ + Ï„''') Ï„' Ï„'')
                (Ï„-substË¢ {Ï„ = Ï„ + (Ï„''' + Ï„') + Ï„''} {Ï„' = Ï„ + Ï„''' + Ï„' + Ï„''}
                  (cong (_+ Ï„'') (sym (+-assoc Ï„ Ï„''' Ï„')))
                  (Î¼Ë¢ (delay-node Ï„ (Î¼Ë¢ k))))}
          (trans
            (trans
              (cong (Î» p â†’ Ï„-substË¢ p (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k)))) uip)
              (sym (Ï„-substË¢-trans _ (+-assoc (Ï„ + Ï„''') Ï„' Ï„'') (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k))))))
            (cong (Ï„-substË¢ (+-assoc (Ï„ + Ï„''') Ï„' Ï„''))
              (sym (Ï„-substË¢-trans _ (cong (_+ Ï„'') (sym (+-assoc Ï„ Ï„''' Ï„'))) (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k)))))))
          (cong (Ï„-substË¢ (+-assoc (Ï„ + Ï„''') Ï„' Ï„''))
            (trans
              (trans
                (Ï„-substË¢-trans _ (cong (_+ Ï„'') (sym (+-assoc Ï„ Ï„''' Ï„'))) (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k))))
                (trans
                  (cong (Î» p â†’ Ï„-substË¢ p (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k)))) uip)
                  (sym (Ï„-substË¢-trans _ (cong (_+ Ï„'') (sym (+-assoc Ï„ Ï„''' Ï„'))) (delay-node Ï„ (Î¼Ë¢ (Î¼Ë¢ k)))))))
              (Ï„-substË¢-Î¼Ë¢ (sym (+-assoc Ï„ Ï„''' Ï„')) (delay-node Ï„ (Î¼Ë¢ k))))))))

Î¼áµ€-assoc : âˆ€ {A Ï„ Ï„' Ï„''}
         â†’  Î¼áµ€ {A} {Ï„} {Ï„' + Ï„''} âˆ˜áµ— Tá¶  Î¼áµ€
         â‰¡áµ— Ï„-substáµ€ (+-assoc Ï„ Ï„' Ï„'') âˆ˜áµ— (Î¼áµ€ âˆ˜áµ— Î¼áµ€)
Î¼áµ€-assoc {A} {Ï„} {Ï„'} {Ï„''} =
  eqáµ— (Î» c â†’
    trans
      (âˆ˜áµ—-reveal Î¼áµ€ (Tá¶  Î¼áµ€) c)
      (trans
        (trans
          (Î¼Ë¢-assoc c)
          (sym
            (cong (map-carrier (Ï„-substáµ€ (+-assoc Ï„ Ï„' Ï„'')))
              (âˆ˜áµ—-reveal _ _ c))))
        (sym
          (âˆ˜áµ—-reveal _ _ c))))






















---------------------------------------------------------------------------------------------------------
---------------------------------------------------------------------------------------------------------


{-


-- Candidate for object-mapping of the underlying functor to support quotienting by delay-node equations
-- NOTE: quick sketch, does not include naturality condition for operation op-nodes


mutual
  
  data TË¢ (A : TSet) : (Ï„ : Time) â†’ (t : Time) â†’ Set where  -- 1st time index (Ï„) is the duration of the computation (its time-grading)
                                                            -- 2nd time index (t) is the corresponding TSets' time-index (presheaf index)
    delay-node : âˆ€ {Ï„' t}
          â†’ (Ï„ : Time)
          â†’ 0 < Ï„
          â†’ Tá¶œË¢ A Ï„' (t + Ï„)
          â†’ TË¢ A (Ï„ + Ï„') t
    comp  : âˆ€ {Ï„ t}
          â†’ Tá¶œË¢ A Ï„ t
          â†’ TË¢ A Ï„ t
  data Tá¶œË¢ (A : TSet) : (Ï„ : Time) â†’ (t : Time) â†’ Set where  -- 1st time index (Ï„) is the duration of the computation (its time-grading)
                                                             -- 2nd time index (t) is the corresponding TSets' time-index (presheaf index)
    leaf  : âˆ€ {t}
          â†’ carrier A t
          â†’ Tá¶œË¢ A 0 t
     
    op-node  : âˆ€ {Ï„ t}
          â†’ (op : Op)
          â†’ carrier âŸ¦ param op âŸ§áµ t
          â†’ ({t' : Time} â†’ t + op-time op â‰¤ t'
                         â†’ carrier âŸ¦ arity op âŸ§áµ t'
                         â†’ TË¢ A Ï„ t')
          â†’ Tá¶œË¢ A (op-time op + Ï„) t

-}
